# AWS Snippets

## Variables

```hcl filename="variables.tf"
variable "region" {
  description = "AWS region where resources will be created"
  type        = string
  default     = "us-central-1"
}

variable "owner" {
  type        = string
  description = "Email of project owner"
}

variable "project" {
  type        = string
  description = "Project name"
}

variable "artifact_bucket" {
  description = "S3 bucket for storing project's artifacts (Terraform state, Lambda function code, Lambda layers)"
  type        = string
}
```

Supply values for input variables by creating a `params.tfvars` file:
```hcl filename="params.tfvars"
region          = ""
owner           = ""
project         = ""
artifact_bucket = ""
```

And pass the file when running Terraform apply:
```sh
terraform apply -var-file="params.tfvars" --auto-approve
```


## Provider

```hcl filename="provider.tf"
terraform {
  required_providers {
    aws = {
      source = "hashicorp/aws"
    }

    # provides a data source that can create archives from individual files or collections of files
    # useful for packaging code files to be deployed
    archive = {
      source  = "hashicorp/archive"
      version = "~> 2.4.2"
    }
  }
}

provider "aws" {
  region = var.region

  default_tags {
    tags = {
      owner   = var.owner
      project = var.project
    }
  }
}
```


## Locals

Here's a list of recommended local values:
- `env`: the current runtime environment to deploy to. We'll use Terraform workspace name for it
- `name_prefix`: prefix to add to resource names (role, lambda function, log group, ...)
- `bucket_prefix`: key prefix to add to s3 paths, for example:
  - `arn:aws:s3:::${local.bucket_prefix}/source/*`
  - `s3://${local.bucket_prefix}/source/*`

```hcl filename="locals.tf"
locals {
  env = terraform.workspace == "default" ? "prod": terraform.workspace
  name_prefix = "${var.project}-${local.env}"
  bucket_prefix = "${var.artifact_bucket}/${var.project}/${local.env}"
}
```


## Backend

*You can skip this section if you want to store Terraform state as a local file on disk.*

Configuration for S3 backend:
```hcl filename="backend.tf"
terraform {
  backend "s3" {
    encrypt = true
  }
}
```

Since a backend block cannot refer to named values (like input variables, locals, or data source attributes). We'll define dynamic arguments in a separated file `backend.tfvars`:

```hcl filename="backend.tfvars"
region               = ""
workspace_key_prefix = ""
bucket               = ""
key                  = ""
dynamodb_table       = ""
```

Then supply it when initialize the working directory:
```sh
terraform init -backend-config=backend.tfvars -reconfigure
```
